                ;*****************************************************
                ;Interrupt vectors.
                ;-----------------------------------------------------
                ;RESET			0000
                ;IE0			0003
                ;TF0			000B
                ;IE1			0013
                ;TF1			001B
                ;RI & TI		0023
                ;TF2 & EXF2		002B
                ;-----------------------------------------------------
                ;*****************************************************
                ;PORT 1
                ;-----------------------------------------------------
                ;P1.0			OUT:	+5V On	
                ;P1.1			OUT:	RST
                ;P1.2			OUT:	SCK
                ;P1.3			OUT:	MISO
                ;P1.4			IN:		MOSI
                ;P1.5
                ;P1.6
                ;P1.7
                ;-----------------------------------------------------
                ;*****************************************************
                ;RAM Locations
                ;-----------------------------------------------------
                ;20				Interrupt flags
                ;21
                ;22
                ;23
                ;24
                ;25				Rom byte to be verified
                ;26				Number of pages
                ;27				PROGROM Action
                ;28				PROGROM Size
                ;29             PROGROM Mode
                ;2A
                ;2B
                ;2C
                ;2D				DPL save
                ;2E				DPH save
                ;2F				Number of verify errors
                ;30-3F
                ;40-4F			Buffer
                ;50-7F			Stack
                ;-----------------------------------------------------
                ;*****************************************************
                ;SCREEN DRIVER
                ;-----------------------------------------------------
                ;01				START SEND ROMDATA.HEX FILE
                ;02				STOP SEND FILE
                ;03				START RECIEVE ROMDATA.HEX FILE
                ;04				STOP RECIEVE FILE
                ;05				START RECIEVE FILE IN 16 BYTE BLOCKS
                ;06				STOP RECIEVE FILE IN 16 BYTE BLOCKS
                ;07				BELL
                ;08				BACK SPACE
                ;09				TAB
                ;0A				LF
                ;0B				LOCATE
                ;0C				HOME
                ;0D				CR
                ;0E				CLS
                ;0F				MODE
                ;10				START SEND CMDFILE.CMD FILE
                ;-----------------------------------------------------
                ;*****************************************************
                				ORG		0000h
0000  01 40     RESET:			AJMP	$START
                ;*****************************************************
0002  FF        				ORG		0003h
0003  20 00 01  IE0IRQ:			JB		$00h,$01h				;$20.0
0006  32        				RETI
0007  02 20 03  				LJMP	$2003h
                ;*****************************************************
000A  FF        				ORG		000Bh
000B  20 01 01  TF0IRQ:			JB		$01h,$01h				;$20.1
000E  32        				RETI
000F  02 20 0B  				LJMP	$200Bh
                ;*****************************************************
0012  FF        				ORG		0013h
0013  20 02 01  IE1IRQ:			JB		$02h,$01h				;$20.2
0016  32        				RETI
0017  02 20 13  				LJMP	$2013h
                ;*****************************************************
001A  FF        				ORG		001Bh
001B  20 03 01  TF1IRQ:			JB		$03h,$01h				;$20.3
001E  32        				RETI
001F  02 20 1B  				LJMP	$201Bh
                ;*****************************************************
0022  FF        				ORG		0023h
0023  20 04 01  RITIIRQ:		JB		$04h,$01h				;$20.4
0026  32        				RETI
0027  02 20 23  				LJMP	$2023h
                ;*****************************************************
002A  FF        				ORG		002Bh
002B  20 05 01  TF2EXF2IRQ:		JB		$05h,$01h				;$20.5
002E  32        				RETI
002F  02 20 2B  				LJMP	$202Bh
                ;*****************************************************
                
0032  FF FF FF  				ORG		0040h
                
0040  75 D0 00  START:			MOV		PSW,#00h
0043  75 A8 00  				MOV		IE,#00h					;Disable all int's
0046  75 81 4F  				MOV		SP,#4Fh					;Init stack pointer. The stack is 48 bytes
0049  75 89 22  				MOV		TMOD,#22h				;T0/T1=8 Bit auto reload
004C  75 8C 1A  				MOV		TH0,#1Ah				;256-230
004F  75 8A 1A  				MOV		TL0,#1Ah
0052  75 8D FD  				MOV		TH1,#0FDh				;256-22118400/(384*9600) (#0FF=57600)
0055  75 8B FD  				MOV		TL1,#0FDh
0058  75 87 80  				MOV		PCON,#80h				;Double baudrate
005B  75 98 76  				MOV		SCON,#76h				;SM0=l
                												;SM1=h
                												;SM2=h
                												;REN=h
                												;TB8=h
                												;RB8=l
                												;TI=h
                												;RI=l
005E  75 20 00  				MOV		$20h,#00h				;RAM int routines ($00-$05,$20.0-$20.5)
0061  75 27 01  				MOV		$27h,#01h				;Action
0064  75 28 01  				MOV		$28h,#01h				;Size
0067  75 29 01  				MOV		$29h,#01h				;Mode
006A  75 88 50  				MOV		TCON,#50h				;T0/T1=On
006D  78 00     				MOV		R0,#00h
006F  90 20 00  				MOV		DPTR,#2000h
0072  79 00     				MOV		R1,#00h
0074  D8 FE     START1:			DJNZ	R0,$START1
0076  D9 FC     				DJNZ	R1,$START1
0078  C2 98     				CLR		SCON.0
007A  31 9B     START2:			ACALL	$HELPMENU
007C  11 FB     START3:			ACALL	$PRNTCRLF
007E  74 3E     				MOV		A,#3Eh
0080  31 93     				ACALL	$TXBYTE
0082  31 8B     START4:			ACALL	$RXBYTE
0084  B4 41 06  				CJNE	A,#41h,$START5
                				;Address input
0087  11 F6     				ACALL	$PRNTCMND
0089  51 29     				ACALL	$ADRINPUT
008B  80 EF     				SJMP	$START3
008D  B4 44 06  START5:			CJNE	A,#44h,$START6
                				;Dump
0090  11 F6     				ACALL	$PRNTCMND
0092  51 2C     				ACALL	$DUMP
0094  80 E4     				SJMP	$START2
0096  B4 45 06  START6:			CJNE	A,#45h,$START7
                				;Enter hex
0099  11 F6     				ACALL	$PRNTCMND
009B  51 58     				ACALL	$ENTERHEX
009D  80 DB     				SJMP	$START2
009F  B4 47 06  START7:			CJNE	A,#47h,$START8
                				;Go
00A2  11 F6     				ACALL	$PRNTCMND
00A4  51 C0     				ACALL	$GO
00A6  80 D2     				SJMP	$START2
00A8  B4 48 04  START8:			CJNE	A,#48h,$START9
                				;Help
00AB  11 F6     				ACALL	$PRNTCMND
00AD  80 CB     				SJMP	$START2
00AF  B4 49 06  START9:			CJNE	A,#49h,$START10
                				;Internal memory
00B2  11 F6     				ACALL	$PRNTCMND
00B4  51 6D     				ACALL	$MEMDUMP
00B6  80 C4     				SJMP	$START3
00B8  B4 4C 06  START10:		CJNE	A,#4Ch,$START11
                				;Load
00BB  11 F6     				ACALL	$PRNTCMND
00BD  51 92     				ACALL	$LOAD
00BF  80 BB     				SJMP	$START3
00C1  B4 50 06  START11:		CJNE	A,#50h,$START12
                				;Program ROM
00C4  11 F6     				ACALL	$PRNTCMND
00C6  51 C4     				ACALL	$EPROM
00C8  80 B0     				SJMP	$START2
00CA  B4 52 06  START12:		CJNE	A,#52h,$START13
                				;Run
00CD  11 F6     				ACALL	$PRNTCMND
00CF  51 C2     				ACALL	$RUN
00D1  80 A7     				SJMP	$START2
00D3  B4 0D AC  START13:		CJNE	A,#0Dh,$START4
                				;CR
00D6  80 A4     				SJMP	$START3
                
                ;RS232 Functions
                ;------------------------------------------------------------------
                
00D8  85 82 2D  PRNTSTR:		MOV		$2Dh,DPL
00DB  85 83 2E  				MOV		$2Eh,DPH
00DE  D0 83     				POP		DPH
00E0  D0 82     				POP		DPL
00E2  E4        PRNTSTR1:		CLR		A
00E3  93        				MOVC	A,@A+DPTR
00E4  A3        				INC		DPTR
00E5  60 04     				JZ		$PRNTSTR2
00E7  31 93     				ACALL	$TXBYTE
00E9  80 F7     				SJMP	$PRNTSTR1
00EB  C0 82     PRNTSTR2:		PUSH	DPL
00ED  C0 83     				PUSH	DPH
00EF  85 2D 82  				MOV		DPL,$2Dh
00F2  85 2E 83  				MOV		DPH,$2Eh
00F5  22        				RET
                
00F6  31 93     PRNTCMND:		ACALL	$TXBYTE
00F8  11 FB     				ACALL	$PRNTCRLF
00FA  22        				RET
                
00FB  74 0D     PRNTCRLF:		MOV		A,#0Dh
00FD  31 93     				ACALL	$TXBYTE
00FF  74 0A     				MOV		A,#0Ah
0101  31 93     				ACALL	$TXBYTE
0103  22        				RET
                
0104  C0 E0     HEXOUT:			PUSH	ACC
0106  C4        				SWAP	A
0107  31 0B     				ACALL	$HEXOUT1
0109  D0 E0     				POP		ACC
010B  54 0F     HEXOUT1:		ANL		A,#0Fh
010D  C3        				CLR		C
010E  94 0A     				SUBB	A,#0Ah
0110  40 02     				JC		$HEXOUT2
0112  24 07     				ADD		A,#07h
0114  24 3A     HEXOUT2:		ADD		A,#3Ah
0116  31 93     				ACALL	$TXBYTE
0118  22        				RET
                
0119  E5 83     HEXDPTR:		MOV		A,DPH
011B  31 04     				ACALL	$HEXOUT
011D  E5 82     				MOV		A,DPL
011F  31 04     				ACALL	$HEXOUT
0121  74 20     				MOV		A,#20h
0123  31 93     				ACALL	$TXBYTE
0125  22        				RET
                
0126  31 32     HEXINPBYTE:		ACALL	$HEXINP
0128  40 07     				JC		$HEXINPBYTE1
012A  C4        				SWAP	A
012B  FB        				MOV		R3,A
012C  31 32     				ACALL	$HEXINP
012E  40 01     				JC		$HEXINPBYTE1
0130  2B        				ADD		A,R3
0131  22        HEXINPBYTE1:	RET
                
0132  31 3E     HEXINP:			ACALL	$HEXINP2
0134  40 07     				JC		$HEXINP1
0136  C0 E0     				PUSH	ACC
0138  EA        				MOV		A,R2
0139  31 93     				ACALL	$TXBYTE
013B  D0 E0     				POP		ACC
013D  22        HEXINP1:		RET
                
013E  31 8B     HEXINP2:		ACALL	$RXBYTE
0140  B4 9F 02  				CJNE	A,#9Fh,$HEXINP3			;Esc
0143  D3        				SETB	C
0144  22        				RET
0145  B4 0D 02  HEXINP3:		CJNE	A,#0Dh,$HEXINP4			;Cr
0148  D3        				SETB	C
0149  22        				RET
014A  FA        HEXINP4:		MOV		R2,A
014B  B4 3A 00  				CJNE	A,#3Ah,$00h
014E  50 08     				JNC		$HEXINP5
0150  B4 30 00  				CJNE	A,#30h,$00h
0153  40 E9     				JC		$HEXINP2
0155  94 30     				SUBB	A,#30h
0157  22        				RET
0158  B4 47 00  HEXINP5:		CJNE	A,#47h,$00h
015B  50 E1     				JNC		$HEXINP2
015D  B4 41 00  				CJNE	A,#41h,$00h
0160  40 DC     				JC		$HEXINP2
0162  94 37     				SUBB	A,#37h
0164  22        				RET
                
0165  31 19     INPDPTR:		ACALL	$HEXDPTR
0167  31 26     				ACALL	$HEXINPBYTE
0169  40 08     				JC		$INPDPTR1
016B  F5 83     				MOV		DPH,A
016D  31 26     				ACALL	$HEXINPBYTE
016F  40 02     				JC		$INPDPTR1
0171  F5 82     				MOV		DPL,A
0173  11 FB     INPDPTR1:		ACALL	$PRNTCRLF
0175  22        				RET
                
0176  C0 01     RX16BYTES:		PUSH	$01h
0178  74 05     				MOV		A,#05h
017A  31 93     				ACALL	$TXBYTE
017C  78 40     				MOV		R0,#40h
017E  79 10     				MOV		R1,#10h
0180  31 8B     RX16BYTES1:		ACALL	$RXBYTE
0182  F6        				MOV		@R0,A
0183  08        				INC		R0
0184  D9 FA     				DJNZ	R1,$RX16BYTES1
0186  D0 01     				POP		$01h
0188  78 40     				MOV		R0,#40h
018A  22        				RET
                
018B  30 98 FD  RXBYTE:			JNB		SCON.0,$RXBYTE
018E  C2 98     				CLR		SCON.0
0190  E5 99     				MOV		A,SBUF
0192  22        				RET
                
0193  30 99 FD  TXBYTE:			JNB		SCON.1,$TXBYTE
0196  C2 99     				CLR		SCON.1
0198  F5 99     				MOV		SBUF,A
019A  22        				RET
                
                ;Functions
                ;------------------------------------------------------------------
                
019B  11 D8     HELPMENU:		ACALL	$PRNTSTR
019D  0E        				DB		0Eh
019E  41 20 41  				DB		'A Address input',0Dh,0Ah
01AF  44 20 44  				DB		'D Dump as hex',0Dh,0Ah
01BE  45 20 45  				DB		'E Enter hex',0Dh,0Ah
01CB  47 20 47  				DB		'G Go (Load and Run)',0Dh,0Ah
01E0  48 20 48  				DB		'H Help',0Dh,0Ah
01E8  49 20 49  				DB		'I Internal memory dump',0Dh,0Ah
0200  4C 20 4C  				DB		'L Load cmd file',0Dh,0Ah
0211  50 20 50  				DB		'P Program ROM',0Dh,0Ah
0220  52 20 52  				DB		'R Run',0Dh,0Ah,00h
0228  22        				RET
                
0229  31 65     ADRINPUT:		ACALL	$INPDPTR
022B  22        				RET
                
022C  C0 82     DUMP:			PUSH	DPL
022E  C0 83     				PUSH	DPH
0230  C0 02     				PUSH	$02h
0232  C0 03     				PUSH	$03h
0234  7B 10     DUMP1:			MOV		R3,#10h
0236  7A 10     DUMP2:			MOV		R2,#10h
0238  31 19     				ACALL	$HEXDPTR
023A  E0        DUMP3:			MOVX	A,@DPTR
023B  31 04     				ACALL	$HEXOUT
023D  74 20     				MOV		A,#20h
023F  31 93     				ACALL	$TXBYTE
0241  A3        				INC		DPTR
0242  DA F6     				DJNZ	R2,$DUMP3
0244  11 FB     				ACALL	$PRNTCRLF
0246  DB EE     				DJNZ	R3,$DUMP2
0248  11 FB     				ACALL	$PRNTCRLF
024A  31 8B     				ACALL	$RXBYTE
024C  B4 9F E5  				CJNE	A,#9Fh,$DUMP1			;Esc
024F  D0 03     				POP		$03h
0251  D0 02     				POP		$02h
0253  D0 83     				POP		DPH
0255  D0 82     				POP		DPL
0257  22        				RET
                
0258  C0 82     ENTERHEX:		PUSH	DPL
025A  C0 83     				PUSH	DPH
025C  31 19     ENTERHEX1:		ACALL	$HEXDPTR
025E  31 26     				ACALL	$HEXINPBYTE
0260  40 06     				JC		$ENTERHEX2
0262  F0        				MOVX	@DPTR,A
0263  A3        				INC		DPTR
0264  11 FB     				ACALL	$PRNTCRLF
0266  80 F4     				SJMP	$ENTERHEX1
0268  D0 83     ENTERHEX2:		POP		DPH
026A  D0 82     				POP		DPL
026C  22        				RET
                
026D  C0 00     MEMDUMP:		PUSH	$00h
026F  78 00     				MOV		R0,#00h
0271  E4        MEMDUMP1:		CLR		A
0272  31 04     				ACALL	$HEXOUT
0274  E8        				MOV		A,R0
0275  31 04     				ACALL	$HEXOUT
0277  74 20     				MOV		A,#20h
0279  31 93     				ACALL	$TXBYTE
027B  E6        MEMDUMP2:		MOV		A,@R0
027C  31 04     				ACALL	$HEXOUT
027E  74 20     				MOV		A,#20h
0280  31 93     				ACALL	$TXBYTE
0282  08        				INC		R0
0283  E8        				MOV		A,R0
0284  54 0F     				ANL		A,#0Fh
0286  70 F3     				JNZ		$MEMDUMP2
0288  11 FB     				ACALL	$PRNTCRLF
028A  E8        				MOV		A,R0
028B  64 80     				XRL		A,#80h
028D  70 E2     				JNZ		$MEMDUMP1
028F  D0 00     				POP		$00h
0291  22        				RET
                
0292  C0 82     LOAD:			PUSH	DPL
0294  C0 83     				PUSH	DPH
0296  C0 00     				PUSH	$00h
0298  C0 03     				PUSH	$03h
029A  7B 80     				MOV		R3,#80h
029C  31 76     LOAD1:			ACALL	$RX16BYTES				;Read 16 bytes from cmd file
029E  31 19     				ACALL	$HEXDPTR
02A0  E6        LOAD2:			MOV		A,@R0
02A1  F0        				MOVX	@DPTR,A
02A2  31 04     				ACALL	$HEXOUT
02A4  74 20     				MOV		A,#20h
02A6  31 93     				ACALL	$TXBYTE
02A8  A3        				INC		DPTR
02A9  08        				INC		R0
02AA  E8        				MOV		A,R0
02AB  64 50     				XRL		A,#50h
02AD  70 F1     				JNZ		$LOAD2					;Not 16 bytes yet
02AF  11 FB     				ACALL	$PRNTCRLF
02B1  DB E9     				DJNZ	R3,$LOAD1				;Not 2K yet
02B3  74 06     				MOV		A,#06h
02B5  31 93     				ACALL	$TXBYTE					;End read 16 bytes from cmd file
02B7  D0 03     				POP		$03h
02B9  D0 00     				POP		$00h
02BB  D0 83     				POP		DPH
02BD  D0 82     				POP		DPL
02BF  22        				RET
                
02C0  51 92     GO:				ACALL	$LOAD
02C2  E4        RUN:			CLR		A
02C3  73        				JMP		@A+DPTR
                
                ;ROM menu selection
                ;------------------------------------------------------------------
                
02C4  71 2E     EPROM:			ACALL	$ROMMENU
02C6  B4 94 64  				CJNE	A,#94h,$EPROMEXIT
02C9  B1 BE     				ACALL	$ROMINSERT
02CB  40 F7     				JC		$EPROM
02CD  12 0A 33  				LCALL	$ROMINIT				;Turn on VCC, pull RST high and init programming mode
02D0  40 F2     				JC		$EPROM					;Initialisation failed
02D2  E5 27     				MOV		A,$27h
02D4  14        				DEC		A
02D5  70 0F     				JNZ		$EPROM2
                				;Test erased
02D7  D1 B1     				ACALL	$ROMWAIT
02D9  E5 29     				MOV		A,$29h
02DB  14        				DEC		A
02DC  70 04     				JNZ		$EPROM1
02DE  D1 C0     				ACALL	$BM_ROMERASED
02E0  80 E2     				SJMP	$EPROM
02E2  F1 91     EPROM1:			ACALL	$PM_ROMERASED
02E4  80 DE     				SJMP	$EPROM
02E6  14        EPROM2:			DEC		A
02E7  70 0F     				JNZ		$EPROM3
                				;Dump to hex file
02E9  D1 B1     				ACALL	$ROMWAIT
02EB  E5 29     				MOV		A,$29h
02ED  14        				DEC		A
02EE  70 04     				JNZ		$EPROM21
02F0  D1 5F     				ACALL	$ROMERASE
02F2  80 D0     				SJMP	$EPROM
02F4  D1 5F     EPROM21:		ACALL	$ROMERASE
02F6  80 CC     				SJMP	$EPROM
02F8  14        EPROM3:			DEC		A
02F9  70 0E     				JNZ		$EPROM4
                				;Dump to screen
02FB  E5 29     				MOV		A,$29h
02FD  14        				DEC		A
02FE  70 04     				JNZ		$EPROM31
0300  F1 2E     				ACALL	$BM_ROMDUMPS
0302  80 C0     				SJMP	$EPROM
0304  12 08 18  EPROM31:		LCALL	$PM_ROMDUMPS
0307  80 BB     				SJMP	$EPROM
0309  14        EPROM4:			DEC		A
030A  70 10     				JNZ		$EPROM5
                				;Verify
030C  D1 B1     				ACALL	$ROMWAIT
030E  E5 29     				MOV		A,$29h
0310  14        				DEC		A
0311  70 04     				JNZ		$EPROM41
0313  F1 56     				ACALL	$BM_ROMVERIFY
0315  80 AD     				SJMP	$EPROM
0317  12 08 50  EPROM41:		LCALL	$PM_ROMVERIFY
031A  80 A8     				SJMP	$EPROM
                EPROM5:			;Program
031C  D1 B1     				ACALL	$ROMWAIT
031E  E5 29     				MOV		A,$29h
0320  14        				DEC		A
0321  70 05     				JNZ		$EPROM51
0323  12 08 C7  				LCALL	$PM_ROMPROG
0326  80 9C     				SJMP	$EPROM
0328  12 08 C7  EPROM51:		LCALL	$PM_ROMPROG
032B  80 97     				SJMP	$EPROM
032D  22        EPROMEXIT:		RET
                
032E  11 D8     ROMMENU:		ACALL	$PRNTSTR
0330  0E        				DB		0Eh
0331  20 20 20  				DB		'   ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿  ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿  ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿',0Dh,0Ah
036D  20 20 20  				DB		'   ³  Action       ³  ³  EEPROM size  ³  ³  Mode         ³',0Dh,0Ah
03A9  20 20 20  				DB		'   ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´  ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´  ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´',0Dh,0Ah
03E5  20 20 20  				DB		'   ³  Test erased  ³  ³  4K           ³  ³  Byte         ³',0Dh,0Ah
0421  20 20 20  				DB		'   ³  Erase        ³  ³  8K           ³  ³  Page         ³',0Dh,0Ah
045D  20 20 20  				DB		'   ³  Screendump   ³  ³  16K          ³  ³               ³',0Dh,0Ah
0499  20 20 20  				DB		'   ³  Verify       ³  ³  32K          ³  ³               ³',0Dh,0Ah
04D5  20 20 20  				DB		'   ³  Program      ³  ³  64K          ³  ³               ³',0Dh,0Ah
0511  20 20 20  				DB		'   ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ',0Dh,0Ah
054D  00        				DB		00h
054E  78 28     				MOV		R0,#28h
0550  B1 A1     				ACALL	$MENUSET
0552  08        				INC		R0
0553  B1 A1     				ACALL	$MENUSET
0555  78 27     				MOV		R0,#27h
0557  B1 5A     				ACALL	$MENUXP
0559  22        				RET
                
055A  B1 A1     MENUXP:			ACALL	$MENUSET
055C  74 08     				MOV		A,#08h
055E  12 01 93  				LCALL	$TXBYTE
0561  12 01 8B  				LCALL	$RXBYTE
0564  B4 9A 0C  				CJNE	A,#9Ah,$MENUXP1
0567  E6        				MOV		A,@R0
0568  14        				DEC		A
0569  60 EF     				JZ		$MENUXP
056B  74 20     				MOV		A,#20h
056D  12 01 93  				LCALL	$TXBYTE
0570  16        				DEC		@R0
0571  80 E7     				SJMP	$MENUXP
0573  B4 9B 0D  MENUXP1:		CJNE	A,#9Bh,$MENUXP2
0576  E6        				MOV		A,@R0
0577  94 05     				SUBB	A,#05h
0579  60 DF     				JZ		$MENUXP
057B  74 20     				MOV		A,#20h
057D  12 01 93  				LCALL	$TXBYTE
0580  06        				INC		@R0
0581  80 D7     				SJMP	$MENUXP
0583  B4 9C 08  MENUXP2:		CJNE	A,#9Ch,$MENUYP1
0586  E8        				MOV		A,R0
0587  94 29     				SUBB	A,#29h
0589  60 CF     				JZ		$MENUXP
058B  08        				INC		R0
058C  80 CC     				SJMP	$MENUXP
058E  B4 9D 08  MENUYP1:		CJNE	A,#9Dh,$MENUYP2
0591  E8        				MOV		A,R0
0592  94 27     				SUBB	A,#27h
0594  60 C4     				JZ		$MENUXP
0596  18        				DEC		R0
0597  80 C1     				SJMP	$MENUXP
0599  B4 9F 01  MENUYP2:		CJNE	A,#9Fh,$MENUYP3			;Esc
059C  22        				RET
059D  B4 94 BA  MENUYP3:		CJNE	A,#94h,$MENUXP			;Insert
05A0  22        				RET
                
05A1  74 0B     MENUSET:		MOV		A,#0Bh
05A3  12 01 93  				LCALL	$TXBYTE
05A6  E6        				MOV		A,@R0
05A7  24 22     				ADD		A,#22h
05A9  12 01 93  				LCALL	$TXBYTE
05AC  E8        				MOV		A,R0
05AD  94 27     				SUBB	A,#27h
05AF  75 F0 13  				MOV		B,#13h
05B2  A4        				MUL		AB
05B3  24 24     				ADD		A,#24h
05B5  12 01 93  				LCALL	$TXBYTE
05B8  74 FB     				MOV		A,#0FBh					;û
05BA  12 01 93  				LCALL	$TXBYTE
05BD  22        				RET
                
                ;------------------------------------------------------------------
                
05BE  12 09 E5  ROMINSERT:		LCALL	$ROMOFF
05C1  11 D8     				ACALL	$PRNTSTR
05C3  0B 2A 23  				DB		0Bh,2Ah,23h,'Insert ',00h
05CE  E5 28     				MOV		A,$28h
05D0  14        				DEC		A
05D1  70 08     				JNZ		$ROMINSERT1
05D3  11 D8     				ACALL	$PRNTSTR
05D5  34 4B 00  				DB		'4K',00h
05D8  75 26 10  				MOV		$26h,#10h				;16 Pages
05DB  14        ROMINSERT1:		DEC		A
05DC  70 08     				JNZ		$ROMINSERT2
05DE  11 D8     				ACALL	$PRNTSTR
05E0  38 4B 00  				DB		'8K',00h
05E3  75 26 20  				MOV		$26h,#20h				;32 Pages
05E6  14        ROMINSERT2:		DEC		A
05E7  70 09     				JNZ		$ROMINSERT3
05E9  11 D8     				ACALL	$PRNTSTR
05EB  31 36 4B  				DB		'16K',00h
05EF  75 26 40  				MOV		$26h,#40h				;64 Pages
05F2  14        ROMINSERT3:		DEC		A
05F3  70 09     				JNZ		$ROMINSERT4
05F5  11 D8     				ACALL	$PRNTSTR
05F7  33 32 4B  				DB		'32K',00h
05FB  75 26 80  				MOV		$26h,#80h				;128 Pages
05FE  14        ROMINSERT4:		DEC		A
05FF  70 09     				JNZ		$ROMINSERT5
0601  11 D8     				ACALL	$PRNTSTR
0603  36 34 4B  				DB		'64K',00h
0607  75 26 00  				MOV		$26h,#00h				;256 Pages
060A  11 D8     ROMINSERT5:		ACALL	$PRNTSTR
060C  20 64 65  				DB		' device and strike <Enter> ',00h
0628  31 8B     				ACALL	$RXBYTE
062A  B4 9F 02  				CJNE	A,#9Fh,$ROMINSERT6	;Esc
062D  D3        				SETB	C
062E  22        				RET
062F  11 D8     ROMINSERT6:		ACALL	$PRNTSTR
0631  0B 2A 23  				DB		0Bh,2Ah,23h,'                                       '
065B  0D 00     				DB		0Dh,00h
065D  C3        				CLR		C
065E  22        				RET
                
065F  74 AC     ROMERASE:		MOV		A,#0ACh
0661  12 09 B2  				LCALL	$ISPCOMM				;Init chip erase byte 1
0664  74 80     				MOV		A,#80h
0666  12 09 B2  				LCALL	$ISPCOMM				;Init chip erase byte 2
0669  E4        				CLR		A
066A  12 09 B2  				LCALL	$ISPCOMM				;Init chip erase byte 3
066D  E4        				CLR		A
066E  12 09 B2  				LCALL	$ISPCOMM				;Init chip erase byte 4
0671  E4        				CLR		A
0672  12 09 AB  				LCALL	$WAIT					;Wait 256 ms
0675  E4        				CLR		A
0676  12 09 AB  				LCALL	$WAIT					;Wait 256 ms
0679  E4        				CLR		A
067A  12 09 AB  				LCALL	$WAIT					;Wait 256 ms
067D  12 08 9B  				LCALL	$PM_ISERASED
0680  50 2B     				JNC		$ROMERASE1
0682  12 09 E5  				LCALL	$ROMOFF					;Set RST low and turn off VCC
0685  12 00 D8  				LCALL	$PRNTSTR
0688  0B 2A 23  				DB		0Bh,2Ah,23h,'Could not erase chip <Enter> ',00h
06A9  12 01 8B  				LCALL	$RXBYTE					;Wait for keypress
06AC  22        				RET
06AD  12 09 E5  ROMERASE1:		LCALL	$ROMOFF					;Set RST low and turn off VCC
06B0  22        				RET
                
06B1  11 D8     ROMWAIT:		ACALL	$PRNTSTR
06B3  0B 2A 23  				DB		0Bh,2Ah,23h,'Wait ...',00h
06BF  22        				RET
                
                
                ;Byte mode
                ;------------------------------------------------------------------
                
06C0  12 0A 43  BM_ROMERASED:	LCALL	$BM_ROMRDBYTE			;Read a byte from ROM
06C3  B4 FF 0E  				CJNE	A,#0FFh,$BM_ROMERASED1	;Not erased
06C6  A3        				INC		DPTR					;Next address
06C7  E5 82     				MOV		A,DPL
06C9  70 F5     				JNZ		$BM_ROMERASED			;Jump if more bytes in this page
06CB  E5 83     				MOV		A,DPH
06CD  B5 26 F0  				CJNE	A,$26h,$BM_ROMERASED	;Jump if more pages
06D0  12 09 E5  				LCALL	$ROMOFF					;Set RST low and turn off VCC
06D3  22        				RET
06D4  12 09 E5  BM_ROMERASED1:	LCALL	$ROMOFF					;Set RST low and turn off VCC
06D7  11 D8     				ACALL	$PRNTSTR
06D9  0B 2A 23  				DB		0Bh,2Ah,23h,'Byte at ',00h
06E5  E5 83     				MOV		A,DPH
06E7  31 04     				ACALL	$HEXOUT					;High address
06E9  E5 82     				MOV		A,DPL
06EB  31 04     				ACALL	$HEXOUT					;Low address
06ED  11 D8     				ACALL	$PRNTSTR
06EF  20 6E 6F  				DB		' not erased <Enter> ',00h
0704  31 8B     				ACALL	$RXBYTE					;Wait for keypress
0706  22        				RET
                
0707  74 03     BM_ROMDUMPF:	MOV		A,#03h
0709  31 93     				ACALL	$TXBYTE					;Init write to file
070B  12 0A 43  BM_ROMDUMPF1:	LCALL	$BM_ROMRDBYTE			;Read a byte from ROM
070E  31 04     				ACALL	$HEXOUT					;Output as hex
0710  74 20     				MOV		A,#20h
0712  31 93     				ACALL	$TXBYTE					;Output a space
0714  A3        				INC		DPTR
0715  E5 82     				MOV		A,DPL
0717  54 0F     				ANL		A,#0Fh
0719  70 02     				JNZ		$BM_ROMDUMPF2			;Still on same line
071B  11 FB     				ACALL	$PRNTCRLF				;Output CRLF
071D  E5 82     BM_ROMDUMPF2:	MOV		A,DPL
071F  70 EA     				JNZ		$BM_ROMDUMPF1			;Jump if more bytes in this page
0721  E5 83     				MOV		A,DPH
0723  B5 26 E5  				CJNE	A,$26h,$BM_ROMDUMPF1	;Jump if more pages
0726  74 04     				MOV		A,#04h
0728  31 93     				ACALL	$TXBYTE					;End write to file
072A  12 09 E5  				LCALL	$ROMOFF					;Set RST low and turn off VCC
072D  22        BM_ROMDUMPF3:	RET
                
072E  12 0A 43  BM_ROMDUMPS:	LCALL	$BM_ROMRDBYTE			;Read a byte from ROM
0731  31 04     				ACALL	$HEXOUT					;Output as hex
0733  74 20     				MOV		A,#20h
0735  31 93     				ACALL	$TXBYTE					;Output a space
0737  A3        				INC		DPTR					;Next ROM address
0738  E5 82     				MOV		A,DPL
073A  54 0F     				ANL		A,#0Fh
073C  70 02     				JNZ		$BM_ROMDUMPS1			;Jump if still on same line
073E  11 FB     				ACALL	$PRNTCRLF				;Output CRLF
0740  E5 82     BM_ROMDUMPS1:	MOV		A,DPL
0742  70 EA     				JNZ		$BM_ROMDUMPS			;Jump if more bytes in this page
0744  11 FB     				ACALL	$PRNTCRLF				;Output CRLF
0746  31 8B     				ACALL	$RXBYTE					;Wait for a keypress
0748  B4 9F 02  				CJNE	A,#9Fh,$BM_ROMDUMPS2
074B  80 05     				SJMP	$BM_ROMDUMPS3
074D  E5 83     BM_ROMDUMPS2:	MOV		A,DPH
074F  B5 26 DC  				CJNE	A,$26h,$BM_ROMDUMPS		;Jump if more pages
0752  12 09 E5  BM_ROMDUMPS3:	LCALL	$ROMOFF					;Set RST low and turn off VCC
0755  22        				RET
                
0756  C0 00     BM_ROMVERIFY:	PUSH	$00h					;Save R0
0758  75 2F 00  				MOV		$2Fh,#00h				;Number of errors
075B  31 76     				ACALL	$RX16BYTES				;Read 16 bytes from cmd file. R0 points to start of 16 byte buffer
075D  86 25     BM_ROMVERIFY1:	MOV		$25h,@R0				;Get byte from buffer
075F  12 0A 43  				LCALL	$BM_ROMRDBYTE			;Read a byte from ROM
0762  B5 25 1B  				CJNE	A,$25h,$BM_ROMVERIFY4	;Compare and jump if not equal
0765  08        BM_ROMVERIFY2:	INC		R0						;Increment buffer pointer
0766  E8        				MOV		A,R0
0767  B4 50 02  				CJNE	A,#50h,$BM_ROMVERIFY3	;Jump if not last byte in buffer
076A  31 76     				ACALL	$RX16BYTES				;Read 16 bytes from cmd file. R0 points to start of 16 byte buffer
076C  A3        BM_ROMVERIFY3:	INC		DPTR					;Next ROM address
076D  E5 82     				MOV		A,DPL
076F  70 EC     				JNZ		$BM_ROMVERIFY1			;Jump if still on same page
0771  E5 83     				MOV		A,DPH
0773  B5 26 E7  				CJNE	A,$26h,$BM_ROMVERIFY1	;Jump if more pages
0776  12 09 E5  				LCALL	$ROMOFF					;Set RST low and turn off VCC
0779  74 06     				MOV		A,#06h
077B  31 93     				ACALL	$TXBYTE					;End read 16 bytes from cmd file
077D  D0 00     				POP		$00h					;Restore R0
077F  22        				RET
0780  12 0A 58  BM_ROMVERIFY4:	LCALL	$ROMVERIFYERR
0783  50 E0     				JNC		$BM_ROMVERIFY2			;Jump if less than 16 errors
0785  12 09 E5  				LCALL	$ROMOFF					;Set RST low and turn off VCC
0788  74 06     				MOV		A,#06h
078A  31 93     				ACALL	$TXBYTE					;End read 16 bytes from cmd file
078C  31 8B     				ACALL	$RXBYTE					;Wait for a keypress
078E  D0 00     				POP		$00h					;Restore R0
0790  22        				RET
                
                ;Page mode
                ;------------------------------------------------------------------
                
0791  74 30     PM_ROMERASED:	MOV		A,#30h
0793  12 09 B2  				LCALL	$ISPCOMM				;Init read page mode
0796  E5 83     				MOV		A,DPH
0798  12 09 B2  				LCALL	$ISPCOMM				;Send high address
079B  E4        PM_ROMERASED1:	CLR		A
079C  12 09 B2  				LCALL	$ISPCOMM				;Get byte from ROM
079F  B4 FF 0E  				CJNE	A,#0FFh,$PM_ROMERASED2	;Jump if not erased
07A2  A3        				INC		DPTR
07A3  E5 82     				MOV		A,DPL
07A5  70 F4     				JNZ		$PM_ROMERASED1			;Jump if more bytes on this page
07A7  E5 83     				MOV		A,DPH
07A9  B5 26 E5  				CJNE	A,$26h,$PM_ROMERASED	;Jump if more pagees
07AC  12 09 E5  				LCALL	$ROMOFF					;Set RST low and turn off VCC
07AF  22        				RET
07B0  12 09 E5  PM_ROMERASED2:	LCALL	$ROMOFF					;Set RST low and turn off VCC
07B3  11 D8     				ACALL	$PRNTSTR
07B5  0B 2A 23  				DB		0Bh,2Ah,23h,'Byte at ',00h
07C1  E5 83     				MOV		A,DPH
07C3  31 04     				ACALL	$HEXOUT					;Output high address as hex
07C5  E5 82     				MOV		A,DPL
07C7  31 04     				ACALL	$HEXOUT					;Output low address as hex
07C9  11 D8     				ACALL	$PRNTSTR
07CB  20 6E 6F  				DB		' not erased <Enter> ',00h
07E0  31 8B     				ACALL	$RXBYTE					;Wait for a keypress
07E2  22        				RET
                
07E3  74 03     PM_ROMDUMPF:	MOV		A,#03h
07E5  31 93     				ACALL	$TXBYTE					;Init Output to hex file
07E7  74 30     PM_ROMDUMPF1:	MOV		A,#30h
07E9  12 09 B2  				LCALL	$ISPCOMM				;Init read page mode
07EC  E5 83     				MOV		A,DPH
07EE  12 09 B2  				LCALL	$ISPCOMM				;Send high address
07F1  E4        PM_ROMDUMPF2:	CLR		A
07F2  12 09 B2  				LCALL	$ISPCOMM				;Get byte from ROM
07F5  31 04     				ACALL	$HEXOUT					;Output as hex
07F7  74 20     				MOV		A,#20h
07F9  31 93     				ACALL	$TXBYTE					;Output a space
07FB  A3        				INC		DPTR
07FC  E5 82     				MOV		A,DPL
07FE  54 0F     				ANL		A,#0Fh
0800  B4 00 07  				CJNE	A,#00h,$PM_ROMDUMPF3	;Jump if more bytes in this line
0803  12 00 FB  				LCALL	$PRNTCRLF				;Output CRLF
0806  E5 82     				MOV		A,DPL
0808  70 E7     				JNZ		$PM_ROMDUMPF2			;Jump if more bytes in this page
080A  E5 83     PM_ROMDUMPF3:	MOV		A,DPH
080C  B5 26 D8  				CJNE	A,$26h,$PM_ROMDUMPF1	;Jump if more pages
080F  74 04     				MOV		A,#04h
0811  12 01 93  				LCALL	$TXBYTE					;End Output to hex file
0814  12 09 E5  				LCALL	$ROMOFF					;Set RST low and turn off VCC
0817  22        				RET
                
0818  74 30     PM_ROMDUMPS:	MOV		A,#30h
081A  12 09 B2  				LCALL	$ISPCOMM				;Init read page mode
081D  E5 83     				MOV		A,DPH
081F  12 09 B2  				LCALL	$ISPCOMM				;Send high address
0822  E4        PM_ROMDUMPS1:	CLR		A
0823  12 09 B2  				LCALL	$ISPCOMM				;Get byte from ROM
0826  12 01 04  				LCALL	$HEXOUT					;Output as hex
0829  74 20     				MOV		A,#20h
082B  12 01 93  				LCALL	$TXBYTE					;Output a space
082E  A3        				INC		DPTR
082F  E5 82     				MOV		A,DPL
0831  54 0F     				ANL		A,#0Fh
0833  70 12     				JNZ		$PM_ROMDUMPS2			;Jump if still on same line
0835  12 00 FB  				LCALL	$PRNTCRLF				;Output CRLF
0838  E5 82     				MOV		A,DPL
083A  70 E6     				JNZ		$PM_ROMDUMPS1			;Jump if more bytes on this page
083C  12 00 FB  				LCALL	$PRNTCRLF				;Output CRLF
083F  12 01 8B  				LCALL	$RXBYTE					;Wait for a keypress
0842  B4 9F 02  				CJNE	A,#9Fh,$PM_ROMDUMPS2
0845  80 05     				SJMP	$PM_ROMDUMPS3			;Esc pressed
0847  E5 83     PM_ROMDUMPS2:	MOV		A,DPH
0849  B5 26 CC  				CJNE	A,$26h,$PM_ROMDUMPS		;Jump if more pages
084C  12 09 E5  PM_ROMDUMPS3:	LCALL	$ROMOFF					;Set RST low and turn off VCC
084F  22        				RET
                
0850  C0 00     PM_ROMVERIFY:	PUSH	$00h					;Save R0
0852  75 2F 00  				MOV		$2Fh,#00h				;Number of errors
0855  12 01 76  				LCALL	$RX16BYTES				;Read 16 bytes from cmd file. R0 points to start of 16 byte buffer
0858  74 30     PM_ROMVERIFY1:	MOV		A,#30h
085A  12 09 B2  				LCALL	$ISPCOMM				;Init read page mode
085D  E5 83     				MOV		A,DPH
085F  12 09 B2  				LCALL	$ISPCOMM				;Send high address
0862  86 25     PM_ROMVERIFY2:	MOV		$25h,@R0				;Get byte from buffer
0864  E4        				CLR		A
0865  12 09 B2  				LCALL	$ISPCOMM				;Get byte from ROM
0868  B5 25 1D  				CJNE	A,$25h,$PM_ROMVERIFY5	;Compare and jump if not equal
086B  08        PM_ROMVERIFY3:	INC		R0						;Increment buffer pointer
086C  E8        				MOV		A,R0
086D  B4 50 03  				CJNE	A,#50h,$PM_ROMVERIFY4	;Jump if not last byte in buffer
0870  12 01 76  				LCALL	$RX16BYTES				;Read 16 bytes from cmd file. R0 points to start of 16 byte buffer
0873  A3        PM_ROMVERIFY4:	INC		DPTR
0874  E5 82     				MOV		A,DPL
0876  70 EA     				JNZ		$PM_ROMVERIFY2			;Jump if still on same page
0878  E5 83     				MOV		A,DPH
087A  B5 26 DB  				CJNE	A,$26h,$PM_ROMVERIFY1	;Jump if more pages
087D  12 09 E5  				LCALL	$ROMOFF					;Set RST low and turn off VCC
0880  74 06     				MOV		A,#06h
0882  12 01 93  				LCALL	$TXBYTE					;End read 16 bytes from cmd file
0885  D0 00     				POP		$00h					;Restore R0
0887  22        				RET
0888  12 0A 58  PM_ROMVERIFY5:	LCALL	$ROMVERIFYERR
088B  50 DE     				JNC		$PM_ROMVERIFY3			;Jump if less than 16 errors
088D  12 09 E5  				LCALL	$ROMOFF					;Set RST low and turn off VCC
0890  74 06     				MOV		A,#06h
0892  12 01 93  				LCALL	$TXBYTE					;End read 16 bytes from cmd file
0895  12 01 8B  				LCALL	$RXBYTE					;Wait for keypress
0898  D0 00     				POP		$00h					;Restore R0
089A  22        				RET
                
089B  90 00 00  PM_ISERASED:	MOV		DPTR,#0000h
089E  75 2F 00  				MOV		$2Fh,#00h
08A1  74 30     PM_ISERASED1:	MOV		A,#30h
08A3  12 09 B2  				LCALL	$ISPCOMM				;Init read page mode
08A6  E5 83     				MOV		A,DPH
08A8  12 09 B2  				LCALL	$ISPCOMM				;Send high address
08AB  E4        PM_ISERASED2:	CLR		A
08AC  12 09 B2  				LCALL	$ISPCOMM				;Get byte from ROM
08AF  04        				INC		A
08B0  42 2F     				ORL		$2Fh,A
08B2  A3        				INC		DPTR
08B3  E5 82     				MOV		A,DPL
08B5  70 F4     				JNZ		$PM_ISERASED2			;Jump if more bytes on this page
08B7  E5 2F     				MOV		A,$2Fh
08B9  70 05     				JNZ		$PM_ISERASED3
08BB  E5 83     				MOV		A,DPH
08BD  B5 26 E1  				CJNE	A,$26h,$PM_ISERASED1	;Jump if more pagees
08C0  C3        PM_ISERASED3:	CLR		C
08C1  E5 2F     				MOV		A,$2Fh
08C3  60 01     				JZ		$PM_ISERASED4
08C5  D3        				SETB	C
08C6  22        PM_ISERASED4:	RET
                
08C7  12 08 9B  PM_ROMPROG:		LCALL	$PM_ISERASED			;Check if chip is erased
08CA  50 4E     				JNC		$PM_ROMPROG1
08CC  74 AC     				MOV		A,#0ACh
08CE  12 09 B2  				LCALL	$ISPCOMM				;Init chip erase byte 1
08D1  74 80     				MOV		A,#80h
08D3  12 09 B2  				LCALL	$ISPCOMM				;Init chip erase byte 2
08D6  E4        				CLR		A
08D7  12 09 B2  				LCALL	$ISPCOMM				;Init chip erase byte 3
08DA  E4        				CLR		A
08DB  12 09 B2  				LCALL	$ISPCOMM				;Init chip erase byte 4
08DE  E4        				CLR		A
08DF  12 09 AB  				LCALL	$WAIT					;Wait 256 ms
08E2  E4        				CLR		A
08E3  12 09 AB  				LCALL	$WAIT					;Wait 256 ms
08E6  E4        				CLR		A
08E7  12 09 AB  				LCALL	$WAIT					;Wait 256 ms
08EA  12 08 9B  				LCALL	$PM_ISERASED
08ED  50 2B     				JNC		$PM_ROMPROG1
08EF  12 09 E5  				LCALL	$ROMOFF					;Set RST low and turn off VCC
08F2  12 00 D8  				LCALL	$PRNTSTR
08F5  0B 2A 23  				DB		0Bh,2Ah,23h,'Could not erase chip <Enter> ',00h
0916  12 01 8B  				LCALL	$RXBYTE					;Wait for keypress
0919  22        				RET
091A  C0 00     PM_ROMPROG1:	PUSH	$00h
091C  90 00 00  				MOV		DPTR,#0000h
091F  12 01 76  				LCALL	$RX16BYTES				;Read 16 bytes from cmd file. R0 points to start of 16 byte buffer
0922  74 40     PM_ROMPROG2:	MOV		A,#40h
0924  12 09 B2  				LCALL	$ISPCOMM				;Init byte programming mode
0927  E5 83     				MOV		A,DPH
0929  12 09 B2  				LCALL	$ISPCOMM				;Send high address
092C  E5 82     				MOV		A,DPL
092E  12 09 B2  				LCALL	$ISPCOMM				;Send low address
0931  E6        				MOV		A,@R0
0932  F5 25     				MOV		$25h,A					;Get byte from buffer
0934  12 09 B2  				LCALL	$ISPCOMM				;Send byte to be programmed
0937  74 1E     				MOV		A,#1Eh
0939  12 09 AB  				LCALL	$WAIT					;Wait 3mS
093C  12 0A 43  				LCALL	$BM_ROMRDBYTE			;Read a byte from ROM
093F  B5 25 22  				CJNE	A,$25h,$PM_ROMPROG4		;Compare and jump if not equal
0942  08        				INC		R0
0943  E8        				MOV		A,R0
0944  B4 50 03  				CJNE	A,#50h,$PM_ROMPROG3
0947  12 01 76  				LCALL	$RX16BYTES				;Read 16 bytes from cmd file. R0 points to start of 16 byte buffer
094A  A3        PM_ROMPROG3:	INC		DPTR
094B  E5 82     				MOV		A,DPL
094D  70 D3     				JNZ		$PM_ROMPROG2			;Jump if still on same page
094F  74 2E     				MOV		A,#2Eh
0951  12 01 93  				LCALL	$TXBYTE
0954  E5 83     				MOV		A,DPH
0956  B5 26 C9  				CJNE	A,$26h,$PM_ROMPROG2		;Jump if more pages
0959  12 09 E5  				LCALL	$ROMOFF					;Set RST low and turn off VCC
095C  74 06     				MOV		A,#06h
095E  12 01 93  				LCALL	$TXBYTE					;End read 16 bytes from cmd file
0961  D0 00     				POP		$00h
0963  22        				RET
0964  C0 E0     PM_ROMPROG4:	PUSH	ACC
0966  12 09 E5  				LCALL	$ROMOFF					;Set RST low and turn off VCC
0969  74 06     				MOV		A,#06h
096B  12 01 93  				LCALL	$TXBYTE					;End read 16 bytes from cmd file
096E  12 00 D8  				LCALL	$PRNTSTR
0971  0B 2A 23  				DB		0Bh,2Ah,23h,'Error at ',00h
097E  E5 83     				MOV		A,DPH
0980  12 01 04  				LCALL	$HEXOUT					;High address
0983  E5 82     				MOV		A,DPL
0985  12 01 04  				LCALL	$HEXOUT					;Low address
0988  74 20     				MOV		A,#20h
098A  12 01 93  				LCALL	$TXBYTE
098D  E5 25     				MOV		A,$25h
098F  12 01 04  				LCALL	$HEXOUT					;Byte from .cmd file
0992  74 20     				MOV		A,#20h
0994  12 01 93  				LCALL	$TXBYTE
0997  D0 E0     				POP		ACC
0999  12 01 04  				LCALL	$HEXOUT					;Byte read from ROM
099C  12 01 8B  				LCALL	$RXBYTE					;Wait for a keypress
099F  D0 00     				POP		$00h					;Restore R0
09A1  22        				RET
                
                ;Wait functions
                ;------------------------------------------------------------------
                
09A2  C0 07     WAIT100:		PUSH	$07h					;Save R7
09A4  7F 5C     				MOV		R7,#5Ch
09A6  DF FE     WAIT1001:		DJNZ	R7,$WAIT1001			;Wait loop, 100uS
09A8  D0 07     				POP		$07h					;Restore R7
09AA  22        				RET
                
09AB  CF        WAIT:			XCH		A,R7
09AC  31 A2     WAIT1:			ACALL	$WAIT100
09AE  DF FC     				DJNZ	R7,$WAIT1
09B0  CF        				XCH		A,R7
09B1  22        				RET
                
                ;Control functions
                ;------------------------------------------------------------------
                
                ;IN A, OUT A
09B2  C0 07     ISPCOMM:		PUSH	$07h
09B4  C0 02     				PUSH	$02h
09B6  7A 08     				MOV		R2,#08h
09B8  33        ISPCOMM1:		RLC		A
09B9  92 93     				MOV		P1.3,C					;MISO
09BB  00        				NOP
09BC  A2 94     				MOV		C,P1.4					;MOSI
09BE  CF        				XCH		A,R7
09BF  33        				RLC		A
09C0  CF        				XCH		A,R7
09C1  D2 92     				SETB	P1.2					;SCK H
09C3  00        				NOP
09C4  00        				NOP
09C5  00        				NOP
09C6  00        				NOP
09C7  C2 92     				CLR		P1.2					;SCK L
09C9  DA ED     				DJNZ	R2,$ISPCOMM1
09CB  EF        				MOV		A,R7
09CC  D0 02     				POP		$02
09CE  D0 07     				POP		$07
09D0  22        				RET
                
09D1  D2 90     ROMON:			SETB	P1.0					;+5V On
09D3  E4        				CLR		A
09D4  31 AB     				ACALL	$WAIT					;Wait 25mS
09D6  E4        				CLR		A
09D7  31 AB     				ACALL	$WAIT					;Wait 25mS
09D9  E4        				CLR		A
09DA  31 AB     				ACALL	$WAIT					;Wait 25mS
09DC  E4        				CLR		A
09DD  31 AB     				ACALL	$WAIT					;Wait 25mS
09DF  D2 91     				SETB	P1.1					;RST H
09E1  E4        				CLR		A
09E2  31 AB     				ACALL	$WAIT					;Wait 25mS
09E4  22        				RET
                
09E5  C2 91     ROMOFF:			CLR		P1.1					;RST L
09E7  E4        				CLR		A
09E8  31 AB     				ACALL	$WAIT					;Wait 25Ms
09EA  75 90 10  				MOV		P1,#10h					;+5V Off, P1.4 As Input
09ED  E4        				CLR		A
09EE  12 09 AB  				LCALL	$WAIT					;Wait 25mS
09F1  22        				RET
                
09F2  74 AC     ROMINITPGM:		MOV		A,#0ACh
09F4  12 09 B2  				LCALL	$ISPCOMM
09F7  74 53     				MOV		A,#53h
09F9  12 09 B2  				LCALL	$ISPCOMM
09FC  74 00     				MOV		A,#00h
09FE  12 09 B2  				LCALL	$ISPCOMM
0A01  74 00     				MOV		A,#00h
0A03  12 09 B2  				LCALL	$ISPCOMM
0A06  B4 69 01  				CJNE	A,#69h,$ROMINITPGM1
0A09  22        				RET
0A0A  12 00 D8  ROMINITPGM1:	LCALL	$PRNTSTR
0A0D  0B 2A 23  				DB		0Bh,2Ah,23h,'Initialisation Error <Enter> ',00h
0A2E  12 01 8B  				LCALL	$RXBYTE
0A31  D3        				SETB	C
0A32  22        				RET
                
0A33  90 00 00  ROMINIT:		MOV		DPTR,#0000h				;DPTR holds ROM address
0A36  12 09 D1  				LCALL	$ROMON					;Turn on VCC and pull RST high
0A39  12 09 F2  				LCALL	$ROMINITPGM
0A3C  50 04     				JNC		$ROMINIT1
0A3E  12 09 E5  				LCALL	$ROMOFF					;Init programming failed
0A41  D3        				SETB	C
0A42  22        ROMINIT1:		RET
                
0A43  74 20     BM_ROMRDBYTE:	MOV		A,#20h
0A45  12 09 B2  				LCALL	$ISPCOMM
0A48  E5 83     				MOV		A,DPH
0A4A  12 09 B2  				LCALL	$ISPCOMM
0A4D  E5 82     				MOV		A,DPL
0A4F  12 09 B2  				LCALL	$ISPCOMM
0A52  74 00     				MOV		A,#00h
0A54  12 09 B2  				LCALL	$ISPCOMM
0A57  22        				RET
                
0A58  C0 E0     ROMVERIFYERR:	PUSH	ACC
0A5A  12 00 D8  				LCALL	$PRNTSTR
0A5D  0D 20 20  				DB		0Dh,'   Error at ',00h
0A6B  E5 83     				MOV		A,DPH
0A6D  12 01 04  				LCALL	$HEXOUT
0A70  E5 82     				MOV		A,DPL
0A72  12 01 04  				LCALL	$HEXOUT
0A75  74 20     				MOV		A,#20h
0A77  12 01 93  				LCALL	$TXBYTE
0A7A  E5 25     				MOV		A,$25h
0A7C  12 01 04  				LCALL	$HEXOUT
0A7F  74 20     				MOV		A,#20h
0A81  12 01 93  				LCALL	$TXBYTE
0A84  D0 E0     				POP		ACC
0A86  12 01 04  				LCALL	$HEXOUT
0A89  12 00 FB  				LCALL	$PRNTCRLF
0A8C  05 2F     				INC		$2Fh
0A8E  E5 2F     				MOV		A,$2Fh
0A90  B4 10 00  				CJNE	A,#10h,$00h
0A93  B3        				CPL		C
0A94  22        				RET
                
                ;------------------------------------------------------------------
                
0A95  FF FF FF  				ORG		2000h					;Fill up 2764
